-- Tests for the grants and math patch

Macro FlywheelComptroller price=1.0 compInitAmount=5000000e18
    Unitroller Deploy
    PriceOracle Deploy Fixed price
    PriceOracleProxy Deploy Admin (PriceOracle Address) (Address Zero) (Address Zero) (Address Zero) (Address Zero) (Address Zero)
    -- Deploy Comptroller
    ComptrollerImpl Deploy Scenario ComptrollerScen
    Unitroller SetPendingImpl ComptrollerScen
    ComptrollerImpl ComptrollerScen Become
    -- Configure Comptroller
    Comptroller SetPriceOracle (PriceOracleProxy Address)
    Comptroller SetCloseFactor 0.5
    Comptroller LiquidationIncentive 1.1
    -- Add markets
    NewCToken ZRX cZRX
    NewCToken BAT cBAT
    Support cZRX collateralFactor:0.5
    Support cBAT collateralFactor:0.5
    -- Setup COMP token
    Erc20 Deploy Standard COMP "COMP Token" 18
    Give (Address Comptroller) compInitAmount COMP
    Comptroller Send "setCompAddress(address)" (Address COMP)

Macro InitSpeeds
    Prep Geoff 100e18 ZRX cZRX
    Mint Geoff 50e18 cZRX--tokenbalance = 50e18 / 2e9 = 2.5e10
    Prep Coburn Some BAT cBAT
    Mint Coburn 6e18 cBAT--tokenbalance = 6e18 / 2e9 = 3e9
    EnterMarkets Coburn cBAT
    Borrow Coburn 1e18 cZRX
    Comptroller SetCompSpeeds (cZRX cBAT) (1 1) (1 1)
    Comptroller RefreshCompSpeeds

Test "COMP speed can be set per market"
    FlywheelComptroller
    InitSpeeds
    -- Baseline comp amounts
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 0
    -- Comp speed can be set
    Comptroller SetCompSpeeds (cZRX) (2) (2)
    FastForward 1000 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 2000
    -- Comp speed can be changed
    Comptroller SetCompSpeeds (cZRX) (4) (4)
    FastForward 1000 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 6000
    -- Comp speed can be removed
    Comptroller SetCompSpeeds (cZRX) (0) (0)
    FastForward 1000 Blocks
    Comptroller ClaimComp Geoff
    Assert Equal (Comptroller CompAccrued Geoff) 0
    Assert Equal (Erc20 COMP TokenBalance Geoff) 6000

Test "Comp is not claimed automatically"
    FlywheelComptroller
    InitSpeeds
    Comptroller SetCompSpeeds (cZRX) (2) (2)
    FastForward 100000 Blocks
    -- Check comp is not claimed automatically
    Mint Geoff 50e18 cZRX
    Assert Equal (Erc20 COMP TokenBalance Geoff) 0
    -- Confirm there was comp to claim
    Comptroller ClaimComp Geoff
    Assert Equal (Erc20 COMP TokenBalance Geoff) 200000
